plugins {
	id 'java'
	id "com.google.protobuf" version "0.8.18"
}

group = 'com.nuvalence'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

var grpcVersion = '1.42.1'
var javaxAnnotationsVersion = '1.3.2'
var protocVersion = '3.17.3'
dependencies {
	if (JavaVersion.current().isJava9Compatible()) {
		// Workaround for @javax.annotation.Generated
		// see: https://github.com/grpc/grpc-java/issues/3633
		compileOnly "javax.annotation:javax.annotation-api:${javaxAnnotationsVersion}"
	}
	implementation("io.grpc:grpc-netty:${grpcVersion}")
	implementation("io.grpc:grpc-protobuf:${grpcVersion}")
	implementation("io.grpc:grpc-stub:${grpcVersion}")
}

sourceSets {
	main {
		proto {
			srcDir("src/main/protobuf")
		}
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protocVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
		all().each { task ->
			task.builtins {
				python { }
			}
		}
	}

}
